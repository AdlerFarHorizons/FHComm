/*
 * Arduino IDE Board Selection: Teensy 3.1 or 3.2
 * Device: MK20DX256VLH7 (NXP K20P64M72SF1 Spec)
 * CPU Speed: 96MHz (optimized) Overclock
 * 
 * RTC: Vbat 1.71V min @ 1uA
 */

/*
 * Serial:  Serial Monitor through USB
 * Serial1: GPS
 * Serial2: RF IN/OUT
 */

#include <TimeLib.h>
#include <SD.h>
#include <SPI.h>
#include <EEPROM.h>

const String PKTPREFIX = "$FH";
const String SOURCEID = "GS0";
const String PKTSUFFIX = "*";
const boolean DEBUG = false;
const int GPSLEN = 100; //max length of GPS output to be sent
const int TERMLEN = 100;
const int CMDLEN = TERMLEN;
const int PKTLEN = 200;
const int TXINTERVAL = 10; // Seconds. Counts of GPS PPS. >=2

const int xtRssiFiltPin = A9;
const int battMonPin = A8;
const int vinMonPin = A7;
const int tempMonPin = A6;
const int xtRxLedPin = 17;
const int xtCmdPin = 16;
const int xtSleepPin = 15;
const int xtNTxPin = 6;
const int sdCsPin = 14;
const int sdCdPin = 5;
const int gpsPpsPin = 2;
const int gpsXstbyPin = 3;
const int xtRssiPwmPin = 4;

const float xtalTol = 20e-6;
const float maxTimeTol = 0.5; // seconds
const long maxTimeSyncAge = (long)( 0.5 + maxTimeTol / xtalTol );


/*
 * Set addresses of EEPROM parameters 
 */
const int eeaddrStart = 256;
const int eeaddrLastGPSTimeSync = eeaddrStart; // time_t
//const int eeaddrXxxx = eeaddrLastGPSTimeSync + sizeof( time_t );


/*
 * Copernicus II configured for two NMEA sentences every two seconds:
 *  ZDA:Provides nothing until first fix, then provides full date
 *      based on internal clock until reset. Clock synced to true
 *      UTC at prior PPS output sometime after a fully valid 2D fix.
 *      
 *  TF: Provides a complete 3D position and velocity fix but no
 *      date/time. Also indicates whether time at last PPS is true
 *      UTC.
 */
const String gpsConfStr = "$PTNLSNM,0020,03*57\r\n";
const String gpsTFReqStr = "$PTNLQTF*45\r\n";

char gps[GPSLEN], gpsBuf[GPSLEN], gpsZDA[GPSLEN], gpsTF[GPSLEN]; 
int gpsIndex = 0;
boolean gpsRdy, gpsZDAFlg, gpsTFFlg, gpsChkFlg, gpsErrFlg, gpsFixFlg;
boolean gpsPosValid, gpsMsgFlg, gpsTimeFlg, gpsTimeValid;
byte gpsChk;
float gpsLat, gpsLon, gpsAlt, gpsVe, gpsVn, gpsVu;
int gpsYr, gpsMon, gpsDay, gpsHr, gpsMin, gpsSec, gpsFixQual;
int ppsCnt;

/*
 * Digi 9XTend configured for Pin Sleep mode, 9600 Baud RF rate, 3 retries, RX LED on
 * valid addresses only.
 */

const String xtConfStr = "ATSM1,BR0,RR3,CD4";

char rx[PKTLEN], rxBuf[PKTLEN];
int rxIndex;
int rssiState;
boolean rxRdy, rxPktFlg, rxChkFlg, rxErrFlg, rxBufCurrent, rxValid;
boolean rxFlg, rxNewFlg;
long rssiStartTime, rssiMeasTime, rssiEndTime;
float rssi;
const float rssiSlope = 10 / 0.15; // db per Duty Cycle
const float rssiIntercept = -100.0 - 0.2 * rssiSlope;

char tx[PKTLEN];
int txIndex = 0;
boolean txPktFlg;

char logFileName[13];
char pkt_buffer[200];
boolean sentencePending, sentenceRdy, sentenceBufRdy;
boolean txFlg, isLogging;
char sentence[GPSLEN];
char sentenceBuf[GPSLEN];
int sentenceIndex = 0;
int rcvdNum;
String msgIn = "";

int numb = 0;
//IntervalTimer txTimer;

File logFile;

void setup(){

  pinMode( xtRxLedPin, INPUT );
  pinMode( xtCmdPin, OUTPUT );
  pinMode( xtSleepPin, OUTPUT );
  pinMode( sdCsPin, OUTPUT );
  pinMode( sdCdPin, INPUT_PULLUP );
  pinMode( gpsPpsPin, INPUT );
  pinMode( gpsXstbyPin, OUTPUT );
  pinMode( xtRssiPwmPin, INPUT );
  pinMode( xtNTxPin, INPUT );

  digitalWrite( xtCmdPin, LOW );
  digitalWrite( xtSleepPin, LOW );
  digitalWrite( gpsXstbyPin, HIGH );

  gpsMsgFlg = false;
  gpsRdy = true;
  txPktFlg = false;
  rxPktFlg = false;
  rxIndex = 0;
  rssiState = 0;
  
  setSyncProvider( getTeensy3Time );
  
  delay( 5000 ); //Wait for GPS to power up.
  Serial.begin(9600);
  Serial1.begin(9600);  
  Serial2.begin(9600); 
  delay( 1000 ); // Give time for serial TX line to bias up

  // Configure XTend
  while( !Serial );
  while( !Serial2 );
  Serial.print( "Entering command mode... " );
  Serial2.print("+++");
  delay( 1100 );
  Serial2.println( xtConfStr );
  Serial2.println( "ATCN" );
  delay( 1000 );
  while( Serial2.available() ) Serial.write( Serial2.read() );
  Serial.println( "" );

  // Clear out serial receive buffers
  while( Serial.available() ) Serial.read();
  while( Serial1.available() ) Serial1.read();
  while( Serial2.available() ) Serial2.read();
  
  // Send GPS Configuration messages
  gpsConfig();
  // Get GPS response
  delay( 200 );
  Serial.println( "waiting for GPS response..." );
  char tmpChar = 0;

  
//  while ( tmpChar != 10 ) {
//    if (Serial1.available() ) {
//      tmpChar = Serial1.read();
//      Serial.write( tmpChar );
//    }
//  }
  Serial.println( "got it." );

  /*
   * Sync library time to RTC. Default is 5 minutes.
   * This is too long.
   */
  setSyncInterval( 10 );
  
  attachInterrupt( gpsPpsPin, ppsSvc, RISING );
//  txTimer.begin(transmit, 10000000);

//  pinMode(5,INPUT_PULLUP);
  delay( 2000 );
  if (!SD.begin( sdCsPin )) {
    Serial.println("Card failed, or not present");
    Serial.println( "SD Card failed or not present. Logging disabled" );
    isLogging = false;
  } else {
    String logFileNameStr = timeToFilename( now() );
    logFileNameStr.toCharArray( logFileName, 13 );
    logFile = SD.open( logFileName, FILE_WRITE );
    if ( logFile ) {
      Serial.println( "Log file " + logFileNameStr + " opened. Logging enabled" );
      isLogging = true;
    } else {
      Serial.println( "Failed to open logfile. Logging disabled" );
      isLogging = false;
    }
  }
  
  if (isLogging ) {
    logFile.println( "\n====== Comm started ======\n" );
    logFile.flush();
  }

}

void loop(){
  // Copy any completed sentence to the buffer, set up for next sentence.
  if ( gpsMsgFlg ) updateGPSMsg();
  if ( gpsZDAFlg ) procZDAMsg();
  if ( gpsTFFlg ) procTFMsg();
  if ( gpsFixFlg ) {
    gpsSendCmd( gpsTFReqStr );
    gpsFixFlg = false;
  }
  if ( Serial1.available() ) getGPSByte();
  if ( Serial2.available() ) getRXByte();
  if ( rxPktFlg ) procRxPkt();
  if ( rssiState == 3 ) {
    rssiState = 0;
    rssi = calcRssi();
  }
  if ( txPktFlg ) makeTxPkt();
  if ( txFlg ) {
    sendData();
    txFlg = false;
  }
}


void getRXByte() {
  // "First Draft" packet acquisition based on simple start and end chars.
  //    No checksum check
  char c = Serial2.read();
  if ( c == '$' || rxRdy ) {
    if ( DEBUG ) Serial.println( "\nRX msg started..." );
    rxRdy = false;
    rxIndex = 0;
  } else {
    if ( c == '*' && !rxRdy ) { // End of in-process RX msg
      rxRdy = true;
      rxPktFlg = true;
      if ( DEBUG ) Serial.println( "\nRX msg ended." );
    } 
  }

  if ( !rxRdy ) {
    rx[rxIndex] = c;
    rxIndex++;
    rx[rxIndex] = 0;
  }

}

void getGPSByte() {
  char c = Serial1.read();
  if ( c == '$' ) { // '$', start of GPS statement - KN
    gpsIndex = 0;
    gpsRdy = false;
    gpsChk = 0;
    if ( DEBUG ) Serial.println( "\nGPS msg started..." );
  }
  if ( c == '*' ) { // '*' indicates end of GPS sentence
    gpsChkFlg = true;
    if ( DEBUG ) {

      if ( DEBUG ) Serial.println( gps );
    }
  }
  if ( !gpsRdy ) {
    if ( gpsChkFlg ) {
      gpsErrFlg = gpsChk != c;
      gpsChkFlg = false;
      gpsRdy = true;
      gpsMsgFlg = true;
    } else {
      gps[gpsIndex] = c;
      gpsChk ^= (byte)c;
    }
    gpsIndex++;
    gps[gpsIndex] = 0;
  }
}

void updateGPSMsg() {
  if ( DEBUG ) Serial.println( "GPS msg rcvd" );
  String str = String( gps );
  int i;
  
  if ( str.substring( 3, 6 ) == "ZDA" ) {
    for ( i = 0; i < GPSLEN ; i++ ) {
      gpsZDA[i] = gps[i];
    }
    gpsZDAFlg = true;
  }

  if ( str.substring( 5, 8 ) == "RTF" ) {
    for ( i = 0; i < GPSLEN ; i++ ) {
      gpsTF[i] = gps[i];
    }
    gpsTFFlg = true;
  }

  for ( int i = 0 ; i < GPSLEN ; i++ ) {
      gpsBuf[i] = gps[i];
  }
  if ( DEBUG ) Serial.println( "GPS Buffer updated." );
  gpsMsgFlg = false;
  if ( DEBUG ) {
    Serial.print( "GPS Flags:" );Serial.print( gpsTFFlg );
    Serial.println( gpsZDAFlg );
  }
  //digitalClockDisplay(now());Serial.println( "");
}

void procZDAMsg() {
  if ( DEBUG ) Serial.println( gpsZDA );
  String utc = getField( gpsZDA, 1, ',' );
  if ( utc.length() > 0 ) {
    gpsHr = utc.substring( 0,2 ).toInt();
    gpsMin = utc.substring( 2,4 ).toInt();
    gpsSec = utc.substring( 4,6 ).toInt();
    gpsDay = getField( gpsZDA, 2, ',' ).toInt();
    gpsMon = getField( gpsZDA, 3, ',' ).toInt();
    gpsYr = getField( gpsZDA, 4, ',' ).toInt();
    gpsTimeFlg = true;
  }
  gpsZDAFlg = false;
}

void procTFMsg() {
  // Need UTC offset with at least 2D fix for valid time sync
  gpsTimeValid = (boolean)getField( gpsTF, 15, ',' ).toInt() &&
                 getField( gpsTF, 5, ',' ).toInt() >= 2;
  // Need 3D position fix for proper pointing.
  gpsPosValid = getField( gpsTF, 5, ',' ) == '3';
  if ( DEBUG ) {
    Serial.print( "gpsPosValid:" );Serial.println( gpsPosValid );
    Serial.print( "gpsTimeValid:" );Serial.println( gpsTimeValid );
  }
  if ( gpsPosValid ) {
    // Get GPS info
    gpsFixQual = getField( gpsTF, 5, ',' ).toInt();
    // Get position, velocity data
    String latStr = getField( gpsTF, 6, ',' );
    gpsLat = latStr.substring( 0,2 ).toFloat();      
    gpsLat = gpsLat + latStr.substring( 2 ).toFloat() / 60.0;
    if ( getField( gpsTF, 7, ',' ) == 'S' ) gpsLat *= -1.0;
    String lonStr = getField( gpsTF, 8, ',' );
    gpsLon = lonStr.substring( 0,3 ).toFloat();
    gpsLon = gpsLon + lonStr.substring( 3 ).toFloat() / 60.0;
    if ( getField( gpsTF, 9, ',' ) == 'W' ) gpsLon *= -1.0;
    gpsAlt = getField( gpsTF, 10, ',' ).toFloat();
    gpsVe = getField( gpsTF, 11, ',' ).toFloat();
    gpsVn = getField( gpsTF, 12, ',' ).toFloat();
    gpsVu = getField( gpsTF, 13, ',' ).toFloat();
  }
  gpsTFFlg = false;
  //txPktFlg = true;
}

void makeTxPkt() {
  String txPktStr =
    SOURCEID + ',' +
    getField( rx, 1, ',' ) + ',' +
    String( rssi, 1 ) + ',' +
    String( now() ) + ',' +
    String( gpsFixQual ) + ',' +
    String( gpsLon,5 ) + ',' +
    String( gpsLat,5 ) + ',' +
    String( gpsAlt ) + ',' +
    String( gpsVe, 3 ).trim() + ',' +
    String( gpsVn, 3 ).trim() + ',' +
    String( gpsVu, 3 ).trim();
    byte chkSum = 0;
    int txLen = txPktStr.length();
    for ( int i = 0 ; i < txLen ; i++ ) {
      chkSum ^=(byte)txPktStr.charAt(i);
    }
    String chkStr = String( chkSum, HEX );
    if ( chkStr.length() == 1 ) chkStr = '0' + chkStr;
    txPktStr = PKTPREFIX + txPktStr + PKTSUFFIX + chkStr;
    txPktStr.toCharArray( tx, PKTLEN );
    txPktFlg = false;
    txFlg = true;
}

void procRxPkt() {
  attachInterrupt( xtRssiPwmPin, rssiStartStop, RISING );
  digitalClockDisplay( now() );Serial.println( " Received..." );  
  Serial.println( rx );
  rxPktFlg = false;
  txPktFlg = true;
}

void rssiStartStop() {
  if ( rssiState == 0 ) {
    rssiStartTime = micros();
    rssiState = 1;
    detachInterrupt( xtRssiPwmPin );
    attachInterrupt( xtRssiPwmPin, rssiMeas, FALLING );
  }
  if ( rssiState == 2 ) {
    rssiEndTime = micros();
    rssiState = 3;
    detachInterrupt( xtRssiPwmPin );
  }
}

void rssiMeas() {
  if ( rssiState == 1 ) {
    rssiMeasTime = micros();
    rssiState = 2;
  }
  detachInterrupt( xtRssiPwmPin );
  attachInterrupt( xtRssiPwmPin, rssiStartStop, RISING );
}

float calcRssi() {
  float dutyCycle = (float)( rssiMeasTime - rssiStartTime ) /
                    (float)( rssiEndTime - rssiStartTime );
  return rssiIntercept + rssiSlope * dutyCycle;
}

String getField( String str, int fieldIndex, char delim ) {
  int startIndex = -1;
  int endIndex = 0;
  for ( int i = 0 ; i <= fieldIndex ; i++ ) {
    startIndex = endIndex + 1;
    endIndex = str.indexOf( delim, startIndex );
  }
  if ( endIndex == -1 ) endIndex = str.length();
  return str.substring( startIndex, endIndex );
}

void sendData() {
  int i = 0;
  digitalClockDisplay( now() );Serial.println( " Sending..." );
  while( tx[i] != 0 ) {
    printAndSendChar( tx[i] );
    i++;
  }
  printAndSendChar( 10 );

  if ( isLogging ) logFile.flush();
}

void printAndSendString( String whatToSend ){ //sends message to both terminal & rf module
  Serial2.print( whatToSend ); // RF module
  Serial.print( whatToSend ); // Terminal
  if ( isLogging ) logFile.print( whatToSend ); // Log file
}

void printAndSendChar( char whatToSend ){ //sends message to both terminal & rf module
  Serial2.write( whatToSend ); // RF module
  Serial.write( whatToSend ); // Terminal
  if ( isLogging ) logFile.write( whatToSend ); // Log file
}

time_t getTeensy3Time() {
  return Teensy3Clock.get();
}

void digitalClockDisplay(time_t t) {
  // digital clock display of the time
  int hr = hour(t);
  if ( hr < 10 ) Serial.print( '0' );
  Serial.print(hr);
  printDigits(minute(t));
  printDigits(second(t));
  Serial.print(" ");
  Serial.print(day(t));
  Serial.print(" ");
  Serial.print(month(t));
  Serial.print(" ");
  Serial.print(year(t)); 
}

void printDigits(int digits){
  // utility function for digital clock display: prints preceding colon and leading 0
  Serial.print(":");
  if(digits < 10) Serial.print('0');
  Serial.print(digits);
}

String timeToFilename( time_t t ) {
  char timeStr[15];
  sprintf( timeStr, "%04i%02i%02i%02i%02i%02i",
           year(t), month(t), day(t), hour(t), minute(t), second(t) );
  String tempStr = (String)timeStr;
  String tempStr2 = tempStr.substring( 2, 10 ) + "." + tempStr.substring( 10, 13 );
  return String( tempStr2 );
}

void gpsConfig() {
  Serial1.print( gpsConfStr );
}

void gpsSendCmd( String cmd ) {
  Serial1.print( cmd );
}

void ppsSvc() {
  long tmpMicros;
  if ( DEBUG ) {
    tmpMicros = micros();
  }
  
  // Set clock(s) to UTC time if a valid fix came in since the last PPS
  if ( DEBUG ) digitalClockDisplay( now() );
  if ( gpsTimeValid && gpsTimeFlg ) {
    time_t oldTime = getTeensy3Time();
    setTime( gpsHr,gpsMin,gpsSec,gpsDay,gpsMon,gpsYr ); // "Library" time    
    time_t newTime = now() + 1;
    setTime( newTime );
    Teensy3Clock.set( newTime ); // Teensy RTC
    time_t temp;
    EEPROM.get(  eeaddrLastGPSTimeSync, temp );
    if ( ( abs( oldTime - newTime ) >= 1 || 
         ( newTime - temp ) >= maxTimeSyncAge ) ) {
      EEPROM.put( eeaddrLastGPSTimeSync, newTime );
      if ( DEBUG ) {
        Serial.print( "Time Corrected:" );      
      }
    }
  }

  // Reset GPS flags
  gpsPosValid = false;
  gpsTimeValid = false;
  gpsTimeFlg = false;
  gpsZDAFlg = false;
  gpsTFFlg = false;

//  if ( ppsCnt == TXINTERVAL - 1 ) {
//    txFlg = true;
//    if ( DEBUG ) Serial.println( " Transmitting..." );
//  }
  if ( ppsCnt == TXINTERVAL - 2 ) {
    gpsFixFlg = true;
    if ( DEBUG ) Serial.println( "Getting GPS fix..." );
  }
  if ( DEBUG ) {
    Serial.print( " ISR duration: " );
    Serial.println( micros() - tmpMicros );
  }
  ppsCnt++;
  if ( ppsCnt == TXINTERVAL ) ppsCnt = 0;
}

String checkStr( String str ) {
  
  char buf[80]; // Max length of NMEA excl. '$',CR,LF = 79, + null
  str.toCharArray(buf, 80);
  byte check = 0x00;
  for ( int i = 0 ; i < str.length() ; i++ ) {
    check ^= (byte)buf[i];
  }
  String chkStr = String( check, HEX );
  if ( check < 0x10 ) {
    chkStr = '0' + chkStr;
  }
  chkStr.toUpperCase();
  return chkStr;
}

